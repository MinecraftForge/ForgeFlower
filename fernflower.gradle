apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'net.minecraftforge.gradleutils'

archivesBaseName = 'forgeflower'
ext {
    CHANGELOG = rootProject.file('build/changelog.txt')
}
 
sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
}

repositories {
    mavenCentral()
    maven { url = "https://libraries.minecraft.net/" }
    maven { url = 'https://maven.minecraftforge.net/' }
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.+'

    testImplementation 'junit:junit:4.+'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation 'com.google.code.gson:gson:2.8.0'
    testImplementation 'net.minecraftforge:DiffPatch:2.0.5:all'
}

// Default all standard Java compile tasks to Java 8
java.toolchain {
    languageVersion = JavaLanguageVersion.of(8)
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

eclipse {
    project {
        name 'ForgeFlower'
    }
}

jar {
    manifest {
        attributes(
            'Main-Class' : 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler'
        )
    }
}

java.withSourcesJar()

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                name = project.archivesBaseName
                packaging = 'jar'
                description = 'Fernflower from https://github.com/JetBrains/intellij-community as standalone library, With Forges Modifications'
                url = 'https://github.com/MinecraftForge/ForgeFlower'
                
                scm {
                    url = 'https://github.com/MinecraftForge/ForgeFlower'
                    connection = 'scm:git:git://github.com/MinecraftForge/ForgeFlower.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/ForgeFlower.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/ForgeFlower/issues'
                }

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}

