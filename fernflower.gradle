apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

archivesBaseName = 'forgeflower'
ext {
    CHANGELOG = rootProject.file('build/changelog.txt')
}
 
sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
    java9.java.srcDirs = ['java9']
}

repositories {
    mavenCentral()
    maven { url = "https://libraries.minecraft.net/" }
}

dependencies {
    // The Java9 code depends on (can see) the "main" source set
    java9Implementation sourceSets.main.output

    testImplementation 'junit:junit:4.+'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation 'com.google.code.gson:gson:2.8.0'
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of(8)
}

// Default all standard Java compile tasks to Java 8
// We'll specify Java 9 only for the java9 compile task
tasks.withType(JavaCompile) {
    options.deprecation = true
}

compileJava9Java {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(9)
    }
}

eclipse {
    classpath {
        sourceSets -= [sourceSets.java9]
    }
    project {
        name 'ForgeFlower'
    }
}

jar {
    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }
    manifest {
        attributes(
            'Main-Class' : 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler',
            'Multi-Release': 'true'
        )
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives sourceJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            if (CHANGELOG.exists()) {
                artifact source: CHANGELOG, classifier: 'changelog'
            }
            artifact tasks.sourceJar

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                name = project.archivesBaseName
                packaging = 'jar'
                description = 'Fernflower from https://github.com/JetBrains/intellij-community as standalone library, With Forges Modifications'
                url = 'https://github.com/MinecraftForge/ForgeFlower'
                
                scm {
                    url = 'https://github.com/MinecraftForge/ForgeFlower'
                    connection = 'scm:git:git://github.com/MinecraftForge/ForgeFlower.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/ForgeFlower.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/ForgeFlower/issues'
                }

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('mavenPassword')) {
                credentials {
                    username = project.properties.mavenUser
                    password = project.properties.mavenPassword 
                }
                url 'http://files.minecraftforge.net/maven/manage/upload'
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}

