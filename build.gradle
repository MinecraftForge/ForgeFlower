buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven/'}
    }
    dependencies {
        classpath 'net.minecraftforge:gitpatcher:0.10.+'
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '2.3.0'
}

apply plugin: 'net.minecraftforge.gitpatcher'

patches {
    submodule = 'FernFlower'
    target = file('ForgeFlower')
    patches = file('FernFlower-Patches')
}

allprojects {
    group = 'net.minecraftforge' //put it under us for now, because this is a unofficial mirrior
    version = gitVersion() // The tag should be OurVersion.FernFlowerCommitNumber, ex: 1.5.380
}

def gitVersion() {
    def desc = grgit.describe(longDescr: true).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name 
    return "${tag}.${offset}${t -> if (branch != 'master' && branch != 'HEAD') t << '-' + branch}"
}

project(':ForgeFlower') {
    apply from: rootProject.file('fernflower.gradle')
    compileJava.dependsOn rootProject.tasks.applyPatches
}

project(':ForgeFlower9') {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    sourceCompatibility = targetCompatibility = 9
    
    sourceSets {
        java9.java.srcDirs = ['../ForgeFlower/java9']
    }
    
    eclipse {
        project {
            name 'FernFlower9'
            linkedResource name: 'java9', type: '2', location: project(':ForgeFlower').file('java9').getAbsolutePath()
        }
        jdt {
            sourceCompatibility = targetCompatibility = 9
        }
    }
    
    dependencies {
        compile project(':ForgeFlower')
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
        sourceCompatibility = 9
        targetCompatibility = 9
        options.compilerArgs.addAll(['--release', '9'])
    }
}

task eclipse() {
    dependsOn('applyPatches', 'ForgeFlower:eclipse', 'ForgeFlower9:eclipse')
}

if (!JavaVersion.current().java9Compatible) {
    throw new RuntimeException("You must build this with JDK 9")
}
