buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven/'}
    }
    dependencies {
        classpath 'net.minecraftforge:gitpatcher:0.10.0.9'
    }
}

plugins {
    id 'net.minecraftforge.gradleutils' version '2.+'
}

apply plugin: 'net.minecraftforge.gitpatcher'

patches {
    submodule = 'FernFlower'
    target = file('ForgeFlower')
    patches = file('FernFlower-Patches')
}

group = 'net.minecraftforge' //put it under us for now, because this is a unofficial mirrior
version = gradleutils.getTagOffsetVersion() // The tag should be OurVersion.FernFlowerCommitNumber, ex: 1.5.380

subprojects {
    group = rootProject.group
    version = rootProject.version

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
    }
}

project(':ForgeFlower') {
    apply from: rootProject.file('fernflower.gradle')
    compileJava.dependsOn rootProject.tasks.applyPatches
    sourcesJar.dependsOn rootProject.tasks.applyPatches
}

project(':ForgeFlower9') {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    sourceSets {
        java9.java.srcDirs = ['../ForgeFlower/java9']
    }

    eclipse {
        project {
            name 'FernFlower9'
            linkedResource name: 'java9', type: '2', location: project(':ForgeFlower').file('java9').getAbsolutePath()
        }
    }

    dependencies {
        implementation project(':ForgeFlower')
    }

    java.toolchain {
        languageVersion = JavaLanguageVersion.of(9)
    }
}

if (tasks.findByName('eclipse') == null) tasks.create('eclipse')
tasks.findByName('eclipse').dependsOn('applyPatches', 'ForgeFlower:eclipse', 'ForgeFlower9:eclipse')

changelog {
    fromMergeBase()
}