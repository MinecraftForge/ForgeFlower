buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven/'}
    }
    dependencies {
        classpath 'net.minecraftforge:gitpatcher:0.10.+'
    }
}

plugins {
    id 'net.minecraftforge.gradleutils' version '2.+'
}

apply plugin: 'net.minecraftforge.gitpatcher'

patches {
    submodule = 'FernFlower'
    target = file('ForgeFlower')
    patches = file('FernFlower-Patches')
}

group = 'net.minecraftforge' //put it under us for now, because this is a unofficial mirrior
version = gradleutils.getTagOffsetVersion() // The tag should be OurVersion.FernFlowerCommitNumber, ex: 1.5.380

logger.lifecycle("${name} version ${version} - JDK ${System.getProperty("java.version")}")

subprojects {
    group = rootProject.group
    version = rootProject.version

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
    }
}

project(':ForgeFlower') {
    apply from: rootProject.file('fernflower.gradle')
    compileJava.dependsOn rootProject.tasks.applyPatches
    sourcesJar.dependsOn rootProject.tasks.applyPatches
}

if (tasks.findByName('eclipse') == null) tasks.create('eclipse')
tasks.findByName('eclipse').dependsOn('applyPatches', 'ForgeFlower:eclipse')
