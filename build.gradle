buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven/'}
    }
    dependencies {
        classpath 'net.minecraftforge:gitpatcher:0.10.+'
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
    }
}

apply plugin: 'net.minecraftforge.gitpatcher'

patches {
    submodule = 'FernFlower'
    target = file('ForgeFlower')
    patches = file('FernFlower-Patches')
}

allprojects {
    group = 'net.minecraftforge' //put it under us for now, because this is a unofficial mirrior
    version = gitVersion() // The tag should be OurVersion.FernFlowerCommitNumber, ex: 1.5.380

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
    }
}

def gitInfo(dir) {
    String.metaClass.rsplit = { String del, int limit = -1 ->
        def lst = new ArrayList()
        def x = 0, idx
        def tmp = delegate
        while ((idx = tmp.lastIndexOf(del)) != -1 && (limit == -1 || x++ < limit)) {
            lst.add(0, tmp.substring(idx + del.length(), tmp.length()))
            tmp = tmp.substring(0, idx)
        }
        lst.add(0, tmp)
        return lst
    }
    
    def git = null
    try {
        git = org.eclipse.jgit.api.Git.open(dir)
    } catch (org.eclipse.jgit.errors.RepositoryNotFoundException e) {
        return [
            tag: '0.0',
            offset: '0',
            hash: '00000000',
            branch: 'master',
            commit: '0000000000000000000000',
            abbreviatedId: '00000000'
        ]
    }
    def desc = git.describe().setLong(true).setTags(true).call().rsplit('-', 2)
    def head = git.repository.exactRef('HEAD')
    def longBranch = head.symbolic ? head?.target?.name : null // matches Repository.getFullBranch() but returning null when on a detached HEAD

    def ret = [:]
    ret.tag = desc[0]
    ret.offset = desc[1]
    ret.hash = desc[2]
    ret.branch = longBranch != null ? org.eclipse.jgit.lib.Repository.shortenRefName(longBranch) : null
    ret.commit = org.eclipse.jgit.lib.ObjectId.toString(head.objectId)
    ret.abbreviatedId = head.objectId.abbreviate(8).name()
    
    return ret
}

def gitVersion() {
    def info = gitInfo(rootProject.file('.'))
    def branch = info.branch
    if (branch != null && branch.startsWith('pulls/'))
        branch = 'pr' + branch.rsplit('/', 1)[1]
    if (branch in [null, 'master', 'HEAD'])
        return "${info.tag}.${info.offset}".toString()
    return "${info.tag}.${info.offset}-${branch}".toString()
}

project(':ForgeFlower') {
    apply from: rootProject.file('fernflower.gradle')
    compileJava.dependsOn rootProject.tasks.applyPatches
    sourcesJar.dependsOn rootProject.tasks.applyPatches
}

project(':ForgeFlower9') {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    sourceSets {
        java9.java.srcDirs = ['../ForgeFlower/java9']
    }

    eclipse {
        project {
            name 'FernFlower9'
            linkedResource name: 'java9', type: '2', location: project(':ForgeFlower').file('java9').getAbsolutePath()
        }
    }

    dependencies {
        implementation project(':ForgeFlower')
    }

    java.toolchain {
        languageVersion = JavaLanguageVersion.of(9)
    }
}

if (tasks.findByName('eclipse') == null) tasks.create('eclipse')
tasks.findByName('eclipse').dependsOn('applyPatches', 'ForgeFlower:eclipse', 'ForgeFlower9:eclipse')
